# purl type → candidates {version date prerelease tag? url? sha256?}; each source fills what it
# knows, `versions` completes date/prerelease. All GETs go through http.nu's cache.

use version.nu *
use http.nu *

# all versions the purl's datasource lists
export def versions [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string, date: any, prerelease: bool> {
  match $p.type {
    github => (github $p)
    gitlab => (gitlab $p)
    pypi => (pypi $p)
    cargo => (crates $p)
    npm => (npm $p)
    hackage => (fetch $"https://hackage.haskell.org/package/($p.name)/preferred" $p.name | get normal-version | each {|v| {version: $v} })
    gnu => (listing $"https://ftp.gnu.org/gnu/($p.name)/" $p.name)
    generic => (generic $p)
    visualstudio => (visualstudio $p)
    applesdk => (applesdk $p)
    _ => (error make {msg: $"no datasource for purl type ($p.type)"})
  } | default false prerelease | default null date
    | update prerelease {|r| $r.prerelease or (version is-prerelease $r.version) }
    | where version =~ '^\d'
}

def fetch [url: string, what: string, --max-age: duration = 10min]: nothing -> oneof<string, list<any>, record> {
  let r = http cached $url --max-age $max_age
  if $r.status != 200 { error make {msg: $"($what): GET ($url) → ($r.status)"} }
  $r.body
}

# releases if the project publishes any, else tags. With ?branch=, the tip commit
# of that branch instead, for unstable tracking: one candidate,
# `<base>-unstable-<date>` (nixpkgs scheme, base is the max stable tag or `0`),
# the sha rides along as `rev` (decide also fires when only rev moved)
def github [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  let branch = $p.qualifiers.branch?
  if $branch != null and $branch != "" {
    let c = (fetch $"https://api.github.com/repos/($p.namespace)/($p.name)/commits?sha=($branch)&per_page=1" $p.name | first)
    let day = ($c.commit.committer.date | into datetime | format date "%F")
    return [{version: $"((github-base $p))-unstable-($day)", date: $c.commit.committer.date, rev: $c.sha}]
  }
  let repo = $"https://api.github.com/repos/($p.namespace)/($p.name)"
  let rels = fetch $"($repo)/releases?per_page=20" $repo | where not draft | each {|r|
    {version: (version from-tag $r.tag_name), date: $r.published_at, prerelease: $r.prerelease, tag: $r.tag_name}
  }
  if ($rels | is-not-empty) { return $rels }
  fetch $"($repo)/tags?per_page=100" $repo | each {|t| {version: (version from-tag $t.name), tag: $t.name} }
}

# max stable tag, the `base` for ?branch= unstable versions. `0` when no tag qualifies.
def github-base [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> string {
  try {
    fetch $"https://api.github.com/repos/($p.namespace)/($p.name)/tags?per_page=100" $p.name
    | each {|t| version from-tag $t.name }
    | where $it =~ '^\d'
    | where {|v| not (version is-prerelease $v) }
    | version max | default "0"
  } catch { "0" }
}

# pkg:gitlab/<ns>/<name>[?repository_url=https://gitlab.example.org][&branch=<name>]:
# tags, or with ?branch= the tip commit of that branch (same unstable shape as github)
def gitlab [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  let host = ($p.qualifiers.repository_url? | default "https://gitlab.com")
  let id = ($"($p.namespace)/($p.name)" | url encode --all)
  let branch = $p.qualifiers.branch?
  if $branch != null and $branch != "" {
    let c = (fetch $"($host)/api/v4/projects/($id)/repository/branches/($branch)" $p.name)
    let day = ($c.commit.committed_date | into datetime | format date "%F")
    return [{version: $"((gitlab-base $p $host $id))-unstable-($day)", date: $c.commit.committed_date, rev: $c.commit.id}]
  }
  fetch $"($host)/api/v4/projects/($id)/repository/tags?per_page=100" $p.name | each {|t| {version: (version from-tag $t.name), date: $t.commit?.created_at?, tag: $t.name} }
}

# max stable tag, the `base` for ?branch= unstable versions. `0` when no tag qualifies.
def gitlab-base [p: record, host: string, id: string]: nothing -> string {
  try {
    fetch $"($host)/api/v4/projects/($id)/repository/tags?per_page=100" $p.name
    | each {|t| version from-tag $t.name }
    | where $it =~ '^\d'
    | where {|v| not (version is-prerelease $v) }
    | version max | default "0"
  } catch { "0" }
}

def pypi [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  fetch $"https://pypi.org/pypi/($p.name)/json" $p.name | get releases | items {|v, files|
    let files = $files | where not yanked
    let sdist = $files | where packagetype == sdist | get -o 0
    if ($files | is-not-empty) { {version: $v, date: $files.0.upload_time_iso_8601, url: $sdist.url?, sha256: $sdist.digests?.sha256?} }
  } | compact
}

def crates [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  fetch $"https://crates.io/api/v1/crates/($p.name)/versions" $p.name | get versions | where not yanked | each {|v| {version: $v.num, date: $v.created_at} }
}

def npm [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  let name = [$p.namespace $p.name] | where $it != '' | str join '/'
  let doc = fetch $"https://registry.npmjs.org/($name | str replace '/' '%2f')" $name
  $doc.versions | columns | each {|v| {version: $v, date: ($doc.time | get -o $v)} }
}

# a directory index or download page: <name>-<version>.tar.* links, date from the same line if any
def listing [url: string, name: string, --regex: oneof<string, nothing>]: nothing -> table<version: string> {
  let re = ($regex | default ('(?:^|[/">])' + $name + '-(\d[0-9a-z.]*?)\.(?:tar\.(?:xz|lz|bz2|gz|zst)|zip|tgz)'))
  # every match on a line: JSON listings (download.gnome.org cache.json) are a single line
  fetch $url $name --max-age 6hr | to text | lines | each {|l|
    let date = ($l | parse -r '(\d{4}-\d{2}-\d{2})' | get -o 0.capture0)
    $l | parse -r $re | each {|m| {version: $m.capture0, date: $date} }
  } | flatten | uniq-by version
}

# pkg:generic/<name>?url=…[&regex=…]
# pkg:visualstudio/<major>: the release channel names the current VisualStudio.vsman. Its URL
# is not derivable from the version, so the path below download/pr/ rides in [pin] as `pr`
def visualstudio [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  let m = (fetch $"https://aka.ms/vs/($p.name)/release/channel" "visualstudio channel" | from json | get channelItems | where type == Manifest | first)
  [{version: $m.version, pr: ($m.payloads.0.url | parse -r '/download/pr/(.+)/[^/]+$' | get capture0.0)}]
}

# pkg:applesdk/CLTools_macOSNMOS_SDK: every product in the macOS software update catalog that
# ships that package. The version is the Command Line Tools release in its .pkm metadata, `path`
# (the part of the URL after content/downloads/) rides in [pin] since it is not derivable
const SUCATALOG = "https://swscan.apple.com/content/catalogs/others/index-26-15-14-13-12-10.16-10.15-10.14-10.13-10.12-10.11-10.10-10.9-mountainlion-lion-snowleopard-leopard.merged-1.sucatalog"

def applesdk [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  let urls = (fetch $SUCATALOG "apple sucatalog" --max-age 12hr
    | parse -r $"<string>https://swcdn.apple.com/content/downloads/\(?<path>[^<]+\)/($p.name).pkg</string>" | get path | uniq)
  $urls | each {|path|
    let pkm = (fetch $"https://swdist.apple.com/content/downloads/($path)/($p.name).pkm" $p.name --max-age 12hr)
    let v = ($pkm | parse -r 'pkg-info[^>]* version="(?<v>\d+\.\d+)[.\d]*"' | get -o v.0)
    if $v != null { {version: $v, path: $path} }
  } | compact
}

def generic [p: record<type: string, namespace: string, name: string, qualifiers: record>]: nothing -> table<version: string> {
  if $p.qualifiers.url? == null { error make {msg: $"pkg:generic/($p.name) needs ?url="} }
  listing $p.qualifiers.url $p.name --regex $p.qualifiers.regex?
}
